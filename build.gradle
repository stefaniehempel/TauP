
apply plugin: 'java'
apply plugin: 'signing'

configurations {
    // we need seisFile for sac output, but not all the other functionality
    all*.exclude group: "com.martiansoftware", module: "jsap"
    all*.exclude group: "org.rxtx", module: "rxtx"
    all*.exclude group: "org.codehaus.woodstox", module: "woodstox-core-lgpl"
    all*.exclude group: "net.java.dev.msv", module: "msv-core"
    all*.exclude group: "org.apache.httpcomponents", module: "httpclient"
    all*.exclude group: "mysql", module: "mysql-connector-java"
}

dependencies {
    compile project(':seisFile')
//    compile 'edu.sc.seis:seisFile:1.7.4'
}

group = 'edu.sc.seis'
version = '2.4.5-SNAPSHOT'

artifacts {
    archives jar
}

def dirName = project.name+'-'+version

def binDistFiles = copySpec {
    from('build/output') {
        fileMode=0755
        include('bin/**')
    }
    from('build/output') {
        include('lib/**')
    }
}

def distFiles = copySpec {
    with(binDistFiles)
    from('build') {
        include('build.gradle')
        include('settings.gradle')
    }
    from('build/docs') {
        include('javadoc/**')
        into('doc')
    }
    from('.') {
        include('gpl-3.0.txt')
        include('doc/**')
        include('jacl/**')
        include('native/**')
        include('src/**')
        include('README')
        exclude('**/*.svn')
    }
    from('srl') {
        include('taup_srl_with_figs.pdf')
        into('doc')
    }
    from('.') {
        include('gradle/**')
        include('gradlew')
        include('gradlew.bat')
    }
    from('.') {
        fileMode=0755
        include('gradlew')
        into('gradle')
    }
    from('src/main/resources/edu/sc/seis/TauP') {
        include('defaultProps')
        into('doc')
    }
    from('src/main/resources/edu/sc/seis/TauP') {
        include('StdModels/*.tvel')
        include('StdModels/*.nd')
    }
    from('build/generated-src/modVersion') {
        include('java/**')
        into('src/main')
    }
}

task copyToLib << {
}

task explodeBin(type: Sync, dependsOn: ['makeScript', 'genModels']) {
    with binDistFiles
    into file("$buildDir/explode")
}

task tarBin(type: Tar, dependsOn: ['explodeBin']) {
    compression = Compression.GZIP
    into(dirName) {
        with binDistFiles
    }
}

task explodeDist(type: Sync, dependsOn: ['explodeBin', 'javadoc', 'modVersionClass', 'wrapper', 'createBuildScript']) {
    with distFiles
    into file("$buildDir/explode")
}

task tarDist(type: Tar, dependsOn: ['explodeDist']) {
    compression = Compression.GZIP
    into(dirName) {
        with distFiles
    }
}

signing {
    sign configurations.archives
}

task makeScript(dependsOn: copyToLib) << { task ->
    def doBat = true
    ScriptBuilder.create('taup_time', 'edu.sc.seis.TauP.TauP_Time', task.project, bat:doBat, yourkit:false)
    ScriptBuilder.create('taup_pierce', 'edu.sc.seis.TauP.TauP_Pierce', task.project, bat:doBat)
    ScriptBuilder.create('taup_path', 'edu.sc.seis.TauP.TauP_Path', task.project, bat:doBat)
    ScriptBuilder.create('taup_create', 'edu.sc.seis.TauP.TauP_Create', task.project, bat:doBat)
    ScriptBuilder.create('taup_curve', 'edu.sc.seis.TauP.TauP_Curve', task.project, bat:doBat)
    ScriptBuilder.create('taup_setsac', 'edu.sc.seis.TauP.TauP_SetSac', task.project, bat:doBat)
    ScriptBuilder.create('taup_wavefront', 'edu.sc.seis.TauP.TauP_Wavefront', task.project, bat:doBat)
    ScriptBuilder.create('taup_table', 'edu.sc.seis.TauP.TauP_Table', task.project, bat:doBat)
    ScriptBuilder.create('taup', 'edu.sc.seis.TauP.TauP', task.project, bat:doBat)
//    ScriptBuilder.create('taup_console', 'edu.sc.seis.TauP.TauP_Console', task.project, bat:doBat)
}


def generatedSrcDir = new File(project.buildDir, 'generated-src/StdModels')
def resourceDir = new File(generatedSrcDir, '/resources')
def outDir = new File(resourceDir, 'edu/sc/seis/TauP/StdModels/')

task genModels(dependsOn: compileJava) << {
    println "Generate models"
    outDir.mkdirs()
    def inDir = new File(getProjectDir(), 'src/main/resources/edu/sc/seis/TauP/StdModels/')
    String[] tvelModelNames = ['iasp91', 'ak135']
    String[] ndModelNames = ['prem']
    def classLoader = new GroovyClassLoader(Project.class.classLoader)
    classLoader.addURL(new File(getBuildDir(), '/classes/java/main').toURL())
    configurations.default.each { File file -> classLoader.addURL(file.toURL())}
    def taupCreate = classLoader.loadClass('edu.sc.seis.TauP.TauP_Create').newInstance()
    taupCreate.setDirectory(inDir.getPath())
    taupCreate.setVelFileType('tvel')
    def vMod
    def tMod
    tvelModelNames.each { String model ->
        taupCreate.setModelFilename(model)
        vMod = taupCreate.loadVMod()
        tMod = taupCreate.createTauModel(vMod)
        tMod.writeModel(new File(outDir, model+'.taup').path)
    }
    taupCreate.setVelFileType('nd')
    ndModelNames.each { String model ->
        taupCreate.setModelFilename(model)
        vMod = taupCreate.loadVMod()
        tMod = taupCreate.createTauModel(vMod)
        tMod.writeModel(new File(outDir, model+'.taup').path)
    }
    // qdt with bigger tol.
    taupCreate.setVelFileType('tvel')
    taupCreate.setMinDeltaP(0.5)
    taupCreate.setMaxDeltaP(50.0)
    taupCreate.setMaxDepthInterval(915.0)
    taupCreate.setMaxRangeInterval(10.0)
    taupCreate.setMaxInterpError(1.0)
    taupCreate.setAllowInnerCoreS(false)
    taupCreate.setModelFilename("iasp91")
    vMod = taupCreate.loadVMod()
    vMod.setModelName("qdt")
    tMod = taupCreate.createTauModel(vMod)
    tMod.writeModel(new File(outDir, 'qdt.taup').path)
}

genModels.inputs.files 'src/main/resources/edu/sc/seis/TauP/defaultProps'
genModels.inputs.files 'src/main/resources/edu/sc/seis/TauP/StdModels/ak135.tvel'
genModels.inputs.files 'src/main/resources/edu/sc/seis/TauP/StdModels/iasp91.tvel'
genModels.inputs.files 'src/main/resources/edu/sc/seis/TauP/StdModels/prem.nd'
genModels.outputs.files new File(outDir, 'ak135.taup')
genModels.outputs.files new File(outDir, 'iasp91.taup')
genModels.outputs.files new File(outDir, 'prem.taup')
genModels.outputs.files new File(outDir, 'qdt.taup')

assemble.dependsOn(tarDist)

sourceSets {
    StdModels {
        resources { resourceDir }
    }
    main {
       resources.srcDirs += resourceDir 
    }
}
processResources.dependsOn(genModels)
